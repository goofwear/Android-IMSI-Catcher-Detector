apply plugin: 'com.android.application'

def gitSha = 'na'
try {
    gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
} catch (Exception e) {
    logger.warn("git not available")
}

def isCi = "true".equals(System.getenv("CI"))
def isTravis = "true".equals(System.getenv("TRAVIS"))

def buildNumber = null
if(isTravis) {
    buildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
} else {
    buildNumber = System.getenv("BUILDOZER_BUILDNUMBER")
}

version = '0.1.37-alpha'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19 // Do not change: Working Icons on Android 5+
        versionCode 37
        testApplicationId "com.SecUpwN.AIMSICD.test"

        buildConfigField 'String', 'BUILD_NUMBER', (buildNumber == null ? 'null' : "\"${buildNumber}\"")
    }
    buildTypes {
        debug {
            try {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '\"' + open_cellid_api_key + '\"'
            } catch (MissingPropertyException e) {
                buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            }
            if (!gitSha.equals('na')) {
                versionNameSuffix '-' + gitSha
            } else {
                versionNameSuffix '-debug'
            }
        }
        release {
            buildConfigField 'String', 'OPEN_CELLID_API_KEY', '"NA"'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            versionNameSuffix '-release'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        warning 'MissingTranslation', 'InvalidPackage'
        abortOnError false

        xmlReport false
        htmlReport !isCi

        textReport isCi
        textOutput 'stdout'
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    productFlavors {
        system {
            versionName "$project.version-system"
        }
        normal {
            versionName "$project.version-normal"
        }
    }
}

dependencies {
    // DO NOT REMOVE BELOW COMMENTED-OUT CODE BEFORE ASKING!
    //compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile 'com.android.support:appcompat-v7:22.2.1'
    //https://github.com/lp0/slf4j-android
    compile 'com.github.Stericson:RootShell:d708ae1f0a'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'eu.lp0.slf4j:slf4j-android:1.7.12-0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'au.com.bytecode:opencsv:2.4'

    //This git hash resolves to version 5.5
    compile 'com.github.MKergall.osmbonuspack:OSMBonusPack:2e8bca20f7'
    compile 'com.github.kaichunlin.transition:core:0.9.2'
    compile 'io.freefair.android-util:logging:1.1.0'
    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
